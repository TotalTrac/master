SET QUOTED_IDENTIFIER, ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[CustomPagesGetAutoComplete]
	@ID				int
	, @Substring	nvarchar(255)
AS
BEGIN
			
	SET NOCOUNT ON;

    WITH CTE (CLIENT_ID)
	AS
	(
		(SELECT c.CLIENT_ID
		FROM [dbo].[Clients] AS c
			INNER JOIN [dbo].[Relationships] AS r
				ON c.CLIENT_ID = r.RELATIONSHIP_CLIENTID
		WHERE 
			c.CLIENT_PARENTID IS NULL
			AND r.RELATIONSHIP_USERID = @ID

			UNION
		
		SELECT 
			c.CLIENT_ID
		FROM [dbo].[Clients] AS c
			INNER JOIN [dbo].[Users] AS u
				ON c.CLIENT_ID = u.USER_CLIENT_ID
		WHERE 
			u.USER_USERID = @ID
		)
			
		UNION ALL

		SELECT 
			c.CLIENT_ID
		FROM [dbo].[Clients] AS c
			INNER JOIN [dbo].[Relationships] AS r
				ON c.CLIENT_ID = r.RELATIONSHIP_CLIENTID
			INNER JOIN CTE
				ON CTE.CLIENT_ID = c.CLIENT_PARENTID
		WHERE 
			c.CLIENT_PARENTID IS NOT NULL
			AND r.RELATIONSHIP_USERID = @ID
	)
	SELECT	
		c.CUSTOMPAGE_ID
		, c.CUSTOMPAGE_CLIENTID
		, c.CUSTOMPAGE_DESCRIPTION
		, c.CUSTOMPAGE_TITLE
		, c.CUSTOMPAGE_URL
		, c.CUSTOMPAGE_ROWVERSION
	FROM 
		[dbo].[CustomPages] AS c
		INNER JOIN CTE
			ON CTE.CLIENT_ID = c.CUSTOMPAGE_CLIENTID
	WHERE
		c.CUSTOMPAGE_DESCRIPTION LIKE '%' + @Substring + '%'
		OR c.CUSTOMPAGE_TITLE LIKE '%' + @Substring + '%'		
END
GO