SET QUOTED_IDENTIFIER, ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[MailPieceGetByBarcode]
	@Barcode				char(31)
	, @SegmentID			int  = NULL
	, @ExpirationThreshold	int	= NULL
	--WITH RECOMPILE
AS
BEGIN
	SET NOCOUNT ON;
    
	DECLARE @TrimmedBarcode nvarchar(31); 
	SET @TrimmedBarcode = LTRIM(RTRIM(@Barcode));

	DECLARE @Length int;
	SET @Length = LEN(@TrimmedBarcode);

	DECLARE @TrackingCode char(20);
	DECLARE @RoutingCode char(11);

	SET @TrackingCode = SUBSTRING(@TrimmedBarcode, 1, 20);
	SET @RoutingCode = 
		CASE @Length
				--WHEN 20 THEN 
				--	NULL
				WHEN 25 THEN
					SUBSTRING(@TrimmedBarcode, 21, 5)
				WHEN 29 THEN
					SUBSTRING(@TrimmedBarcode, 21, 9)
				WHEN 31 THEN
					SUBSTRING(@TrimmedBarcode, 21, 11)
				ELSE
					NULL
			END;

	SELECT TOP 1
		m.MAILPIECE_ID
		, m.MAILPIECE_ADDRESS1
		, m.MAILPIECE_ADDRESS2
		, m.MAILPIECE_ADDRESS3		
		, m.MAILPIECE_CITY
		, m.MAILPIECE_COMPANY
		, m.MAILPIECE_CRRT
		, m.MAILPIECE_DELIVERYDATE
		, m.MAILPIECE_FINALIZESCANID		
		, m.MAILPIECE_FIRSTNAME
		, m.MAILPIECE_FIRSTSCANID
		, m.MAILPIECE_FORWARDSCANID
		, m.MAILPIECE_GROUPID
		, m.MAILPIECE_INDIVIDUALID
		, m.MAILPIECE_LASTNAME	
		, m.MAILPIECE_LASTSCANID	
		, m.MAILPIECE_RETURNSCANID
		, m.MAILPIECE_ROUTINGCODE
		, m.MAILPIECE_SEGMENTID
		, m.MAILPIECE_SCFID
		, m.MAILPIECE_STATE
		, m.MAILPIECE_TARGETID
		, m.MAILPIECE_TITLE
		, m.MAILPIECE_TRACKINGCODE
		, m.MAILPIECE_ZIPCODE
		, m.MAILPIECE_ZIPCODEEXT		
	FROM
		[dbo].[MailPieces] AS m
		INNER JOIN [dbo].[MailSegments] AS s
			ON m.MAILPIECE_SEGMENTID = s.MAILSEGMENT_ID
	WHERE
		@TrackingCode = m.MAILPIECE_TRACKINGCODE
		AND (@RoutingCode IS NULL OR @RoutingCode = m.MAILPIECE_ROUTINGCODE)
		AND (@SegmentID IS NULL OR m.MAILPIECE_SEGMENTID = @SegmentID)
		AND (@ExpirationThreshold IS NULL OR @ExpirationThreshold >= DATEDIFF(DAY, s.MAILSEGMENT_DROPDATE, GETUTCDATE()))
	ORDER BY
		m.MAILPIECE_ID DESC
	--OPTION (RECOMPILE)	
END
GO