SET QUOTED_IDENTIFIER, ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[MailSegmentsGetAutoComplete]
	@ID				int
	, @Substring	nvarchar(255)
AS
BEGIN
			
	SET NOCOUNT ON;

    WITH CTE (CLIENT_ID, CLIENT_NAME, CLIENT_SHORTNAME)
	AS
	(
		(SELECT c.CLIENT_ID, c.CLIENT_NAME, c.CLIENT_SHORTNAME
		FROM [dbo].[Clients] AS c
			INNER JOIN [dbo].[Relationships] AS r
				ON c.CLIENT_ID = r.RELATIONSHIP_CLIENTID
		WHERE 
			c.CLIENT_PARENTID IS NULL
			AND r.RELATIONSHIP_USERID = @ID

			UNION
		
		SELECT 
			c.CLIENT_ID, c.CLIENT_NAME, c.CLIENT_SHORTNAME
		FROM [dbo].[Clients] AS c
			INNER JOIN [dbo].[Users] AS u
				ON c.CLIENT_ID = u.USER_CLIENT_ID
		WHERE 
			u.USER_USERID = @ID
		)
			
		UNION ALL

		SELECT 
			c.CLIENT_ID, c.CLIENT_NAME, c.CLIENT_SHORTNAME
		FROM [dbo].[Clients] AS c
			INNER JOIN [dbo].[Relationships] AS r
				ON c.CLIENT_ID = r.RELATIONSHIP_CLIENTID
			INNER JOIN CTE
				ON CTE.CLIENT_ID = c.CLIENT_PARENTID
		WHERE 
			c.CLIENT_PARENTID IS NOT NULL
			AND r.RELATIONSHIP_USERID = @ID
	)	
	SELECT 
		s.MAILSEGMENT_ID
		, s.MAILSEGMENT_ACCESSIBILITYLEVELID		
    , s.MAILSEGMENT_BILLINGSTATUS
    , s.MAILSEGMENT_BILLINGINVOICEID
    , s.MAILSEGMENT_COMMENTTHREADID
		, s.MAILSEGMENT_COMMINGLERID
		, s.MAILSEGMENT_COMPLETEMAIL
		, s.MAILSEGMENT_CREATED
		, s.MAILSEGMENT_CREATEDBYID
		, s.MAILSEGMENT_CRIDID
		, s.MAILSEGMENT_DATADUE
		, s.MAILSEGMENT_DESCRIPTION
		, s.MAILSEGMENT_DESTINATIONANALYSIS
		, s.MAILSEGMENT_DROPDATE
		, s.MAILSEGMENT_ENTRYPOINT
		, s.MAILSEGMENT_ESTQTY		
		, s.MAILSEGMENT_FIRSTFINALIZESCANID
		, s.MAILSEGMENT_FIRSTSCANID		
		, s.MAILSEGMENT_COMMINGLERSEGMENTID
		, s.MAILSEGMENT_JOBID
		, s.MAILSEGMENT_LASTFINALIZESCANID		
		, s.MAILSEGMENT_LASTSCANID
		, s.MAILSEGMENT_ISMAILANYWHERE
		, s.MAILSEGMENT_MAILANYWHEREACCTNO
		, s.MAILSEGMENT_MATERIALDUE		
		, s.MAILSEGMENT_MATCHMAIL
		, s.MAILSEGMENT_METERRATE
		, s.MAILSEGMENT_NAME		
		, s.MAILSEGMENT_NONPROFIT	
		, s.MAILSEGMENT_OUTGOING		
		, s.MAILSEGMENT_PIECEHEIGHT
		, s.MAILSEGMENT_PIECELENGTH
		, s.MAILSEGMENT_PIECETHICKNESS
		, s.MAILSEGMENT_PIECETYPEID
		, s.MAILSEGMENT_PIECEWEIGHT		
		, s.MAILSEGMENT_POSTAGEAPPLIEDID
		, s.MAILSEGMENT_POSTALCLASSID			
		, s.MAILSEGMENT_PERMITID
		, s.MAILSEGMENT_PRESORTED
		, s.MAILSEGMENT_MAILSTREAMID
		, s.MAILSEGMENT_SEGMENTTYPEID		
		, s.MAILSEGMENT_SPECIALPREPARATION
		, s.MAILSEGMENT_TARGETDATE
		, s.MAILSEGMENT_TRACKINGTYPEID
		, ( SELECT SUM(DISTINCT VERSION_TRIGGEREVENT) 
			FROM [usps].[MailSegmentEmailVersions] 
			WHERE VERSION_SEGMENTID = s.MAILSEGMENT_ID
		  ) 
		  AS MAILSEGMENT_TRIGGEREVENTS				
		, s.MAILSEGMENT_ROWVERSION	
	FROM		 
		[dbo].[MailSegments] AS s
		INNER JOIN [dbo].[Jobs] As m
			ON s.MAILSEGMENT_JOBID = m.JOB_ID
		INNER JOIN [job].Jobs AS j
			ON m.JOB_JOBID = j.JOB_ID
		INNER JOIN CTE
			ON CTE.CLIENT_ID = j.JOB_CLIENTID
	WHERE
		(s.MAILSEGMENT_NAME LIKE '%' + @Substring + '%')
		OR (m.JOB_NAME LIKE '%' + @Substring + '%')
		OR (j.JOB_NAME LIKE '%' + @Substring + '%')
		OR (CONVERT(char, j.JOB_ID) LIKE '%' + @Substring + '%')
		OR (CLIENT_NAME LIKE '%' + @Substring + '%')
		OR (CLIENT_SHORTNAME LIKE '%' + @Substring + '%')
END
GO