SET QUOTED_IDENTIFIER, ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[colpenn_CoverageCodesGetCountsByBatchDateRange] (  
  @Effort			char(1)	= NULL
  , @DateFrom		datetime = NULL
  , @DateTo			datetime = NULL 
  , @GroupByDate	bit = NULL
)
AS
BEGIN
	SET NOCOUNT ON;
	
	SELECT 
		@Effort AS EFFORT		 
		, t.COVERAGECODE
		, t.EFFORT
		, CASE @GroupByDate WHEN 1 THEN BATCHDATE END AS BATCHDATE
		, CASE @GroupByDate WHEN 1 THEN MAILDATE END AS MAILDATE
		, CASE @GroupByDate WHEN 1 THEN TRANSACTIONDATE END AS TRANSACTIONDATE
		, COUNT(CASE WHEN t.RECORDTYPE = '1' THEN 1 END) AS MAILRECORDS
		, COUNT(CASE WHEN t.RECORDTYPE = '2' THEN 1 END) AS REJECTRECORDS
	FROM 
	(
		SELECT 
			'1' AS RECORDTYPE
			, m.MAILRECORD_COVERAGECODE AS COVERAGECODE
			, m.MAILRECORD_EFFORT AS EFFORT
			, m.MAILRECORD_BATCHDATE AS BATCHDATE
			, m.MAILRECORD_MAILDATE AS MAILDATE
			, m.MAILRECORD_TRANSACTIONDATE AS TRANSACTIONDATE
        FROM 
			colpenn_MailRecords AS m
        WHERE 
			(@DateFrom IS NULL OR m.MAILRECORD_BATCHDATE >= @DateFrom) 
			AND (@DateTo IS NULL OR m.MAILRECORD_BATCHDATE <= @DateTo)
			AND (@Effort IS NULL OR @Effort = '' OR m.MAILRECORD_EFFORT = @Effort)
         
         UNION ALL
         
		SELECT 
			'2' AS RECORDTYPE
			, r.REJECTRECORD_COVERAGECODE AS COVERAGECODE
			, r.REJECTRECORD_EFFORT AS EFFORT
			, r.REJECTRECORD_BATCHDATE AS BATCHDATE
			, r.REJECTRECORD_MAILDATE AS MAILDATE
			, r.REJECTRECORD_TRANSACTIONDATE AS TRANSACTIONDATE
        FROM 
			colpenn_RejectRecords AS r
         WHERE 
			(@DateFrom IS NULL OR r.REJECTRECORD_BATCHDATE >= @DateFrom) 
			AND (@DateTo IS NULL OR r.REJECTRECORD_BATCHDATE <= @DateTo)
			AND (@Effort IS NULL OR @Effort = '' OR r.REJECTRECORD_EFFORT = @Effort)
        ) t
	GROUP BY
		COVERAGECODE,
		EFFORT,
		CASE @GroupByDate WHEN 1 THEN t.TRANSACTIONDATE END,
		CASE @GroupByDate WHEN 1 THEN t.MAILDATE END,
		CASE @GroupByDate WHEN 1 THEN t.BATCHDATE END
	
END
GO