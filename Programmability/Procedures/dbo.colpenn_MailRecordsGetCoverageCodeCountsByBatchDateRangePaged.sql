SET QUOTED_IDENTIFIER, ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[colpenn_MailRecordsGetCoverageCodeCountsByBatchDateRangePaged] (
	@Effort			char(1)		= NULL  
	, @DateFrom		datetime	= NULL
	, @DateTo		datetime	= NULL 
	, @Page			int
	, @RowsPerPage	int  
)
AS
BEGIN
	SET NOCOUNT ON;
	
	DECLARE @StartRow int,
			@EndRow int;

	SET @StartRow = ((@Page - 1) * @RowsPerPage) + 1;
	SET @EndRow = @StartRow + @RowsPerPage - 1;
	
	SELECT
		COVERAGECODE
		, COVERAGECODE_EFFORT
		, COVERAGECODE_COUNT
		, COVERAGECODE_MAILDATE
		, COVERAGECODE_TRANSACTIONDATE
	FROM 
	(
		SELECT 
			ROW_NUMBER() OVER (ORDER BY 
				MAILRECORD_COVERAGECODE
				, MAILRECORD_TRANSACTIONDATE 
				, MAILRECORD_MAILDATE
				, MAILRECORD_EFFORT) AS RESULTNUM			
			, MAILRECORD_COVERAGECODE AS COVERAGECODE
			, MAILRECORD_EFFORT AS COVERAGECODE_EFFORT
			, COUNT(*) AS COVERAGECODE_COUNT		
			, MAILRECORD_MAILDATE AS COVERAGECODE_MAILDATE
			, MAILRECORD_TRANSACTIONDATE AS COVERAGECODE_TRANSACTIONDATE 
		FROM 
			[dbo].[colpenn_MailRecords] 
		WHERE
			(@Effort IS NULL OR @Effort = '' OR MAILRECORD_EFFORT = @Effort)
			AND (@DateFrom IS NULL OR MAILRECORD_BATCHDATE >= @DateFrom)
			AND (@DateTo IS NULL OR MAILRECORD_BATCHDATE <= @DateTo)
		GROUP BY
			MAILRECORD_COVERAGECODE
		, MAILRECORD_TRANSACTIONDATE
		, MAILRECORD_MAILDATE
		, MAILRECORD_EFFORT
	)
	AS NumberResults
	WHERE ResultNum BETWEEN @StartRow AND @EndRow;
END
GO