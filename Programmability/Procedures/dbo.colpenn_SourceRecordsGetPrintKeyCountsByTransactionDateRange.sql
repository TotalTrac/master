SET QUOTED_IDENTIFIER, ANSI_NULLS ON
GO
CREATE PROCEDURE [dbo].[colpenn_SourceRecordsGetPrintKeyCountsByTransactionDateRange]
	@DateFrom	datetime	= NULL
  , @DateTo		datetime	= NULL 	
AS
BEGIN
	SET NOCOUNT ON;

	DECLARE @SourceRecords TABLE
	(		
		PRINTKEY			nvarchar(7)
		, BATCHDATE			datetime NULL
		, MAILDATE			datetime NULL
		, TRANSACTIONDATE	datetime NULL
	)
	
	-- INSERT THE REJECT RECORDS
	INSERT INTO @SourceRecords
	(
		PRINTKEY
		, BATCHDATE
		, MAILDATE
		, TRANSACTIONDATE
	)
	SELECT 
		REJECTRECORD_PRINTKEY, 
		NULL, 
		NULL, 
		REJECTRECORD_TRANSACTIONDATE 
	FROM 
		[dbo].[colpenn_RejectRecords]
	WHERE
		(@DateFrom IS NULL OR REJECTRECORD_TRANSACTIONDATE >= @DateFrom)
		AND (@DateTo IS NULL OR REJECTRECORD_TRANSACTIONDATE <= @DateTo);
	
	-- INSERT THE MAILRECORDS
	INSERT INTO @SourceRecords
	(
		PRINTKEY
		, BATCHDATE
		, MAILDATE
		, TRANSACTIONDATE
	)
	SELECT 
		MAILRECORD_PRINTKEY
		, MAILRECORD_BATCHDATE
		, MAILRECORD_MAILDATE
		, MAILRECORD_TRANSACTIONDATE 
	FROM 
		[dbo].[colpenn_MailRecords]
	WHERE
		(@DateFrom IS NULL OR MAILRECORD_TRANSACTIONDATE >= @DateFrom)
		AND (@DateTo IS NULL OR MAILRECORD_TRANSACTIONDATE <= @DateTo);
	
	-- GET OUR SOURCE RECORDS
    SELECT 
		PRINTKEY AS PRINTKEY
		, NULL AS PRINTKEY_EFFORT
		, COUNT(*) AS PRINTKEY_COUNT
	FROM
		@SourceRecords
	GROUP BY
		PRINTKEY
	;
END
GO