SET QUOTED_IDENTIFIER, ANSI_NULLS ON
GO
CREATE PROCEDURE [usps].[PermitsGetByUserID]
	@ID				int
AS
BEGIN
			
	SET NOCOUNT ON;

    WITH CTE (CLIENT_ID)
	AS
	(
		(SELECT c.CLIENT_ID
		FROM [dbo].[Clients] AS c
			INNER JOIN [dbo].[Relationships] AS r
				ON c.CLIENT_ID = r.RELATIONSHIP_CLIENTID
		WHERE 
			c.CLIENT_PARENTID IS NULL
			AND r.RELATIONSHIP_USERID = @ID

			UNION
		
		SELECT 
			c.CLIENT_ID
		FROM [dbo].[Clients] AS c
			INNER JOIN [dbo].[Users] AS u
				ON c.CLIENT_ID = u.USER_CLIENT_ID
		WHERE 
			u.USER_USERID = @ID
		)
			
		UNION ALL

		SELECT 
			c.CLIENT_ID
		FROM [dbo].[Clients] AS c
			INNER JOIN [dbo].[Relationships] AS r
				ON c.CLIENT_ID = r.RELATIONSHIP_CLIENTID
			INNER JOIN CTE
				ON CTE.CLIENT_ID = c.CLIENT_PARENTID
		WHERE 
			c.CLIENT_PARENTID IS NOT NULL
			AND r.RELATIONSHIP_USERID = @ID
	)	
	SELECT 
		PERMIT_ID
		, PERMIT_ACCOUNTNUMBER
		, PERMIT_ADDRESS1
		, PERMIT_ADDRESS2
		, PERMIT_CITY
		, PERMIT_CLIENTID
		, PERMIT_COMPANY
		, PERMIT_CREATED
		, PERMIT_CREATEDBYID
		, PERMIT_EXPIRATIONDATE
		, PERMIT_GLOBAL
		, PERMIT_NUMBER
		, PERMIT_POSTOFFICE
		, PERMIT_STATE
		, PERMIT_TYPEID
		, PERMIT_ZIPCODE
		, PERMIT_ROWVERSION
	FROM
		[usps].Permits AS p
		INNER JOIN CTE
			ON CTE.CLIENT_ID = p.PERMIT_CLIENTID

	UNION

	SELECT 
		PERMIT_ID
		, PERMIT_ACCOUNTNUMBER
		, PERMIT_ADDRESS1
		, PERMIT_ADDRESS2
		, PERMIT_CITY
		, PERMIT_CLIENTID
		, PERMIT_COMPANY
		, PERMIT_CREATED
		, PERMIT_CREATEDBYID
		, PERMIT_EXPIRATIONDATE
		, PERMIT_GLOBAL
		, PERMIT_NUMBER
		, PERMIT_POSTOFFICE
		, PERMIT_STATE
		, PERMIT_TYPEID
		, PERMIT_ZIPCODE
		, PERMIT_ROWVERSION
	FROM
		[usps].Permits AS p
	WHERE
		p.PERMIT_GLOBAL = 1
END
GO