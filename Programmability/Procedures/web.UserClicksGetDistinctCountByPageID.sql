SET QUOTED_IDENTIFIER, ANSI_NULLS ON
GO
CREATE PROCEDURE [web].[UserClicksGetDistinctCountByPageID] 
	@ID	int
AS
BEGIN
	SET NOCOUNT ON;
	
	DECLARE @Clicks TABLE
	(
		CLICK_USERID int
	)
	
	INSERT INTO @Clicks ( CLICK_USERID )
    (SELECT
		DISTINCT uc.USERLINKCLICK_USERID
    FROM
		[web].[UserLinkClicks] AS uc
		INNER JOIN [web].[LinkClicks] AS lc
			ON uc.USERLINKCLICK_LINKCLICKID = lc.LINKCLICK_ID
		INNER JOIN [web].[Links] AS l
			ON lc.LINKCLICK_LINKID = l.LINK_ID	
		INNER JOIN [web].[Pages] AS p
			ON l.LINK_PAGEID = p.PAGE_ID
	WHERE
		p.PAGE_ID = @ID
		
	UNION ALL
	
	SELECT
		DISTINCT uc.USERBUTTONCLICK_USERID
    FROM
		[web].[UserButtonClicks] AS uc
		INNER JOIN [web].[ButtonClicks] AS lc
			ON uc.USERBUTTONCLICK_BUTTONCLICKID = lc.BUTTONCLICK_ID
		INNER JOIN [web].[Buttons] AS l
			ON lc.BUTTONCLICK_BUTTONID = l.BUTTON_ID	
		INNER JOIN [web].[Pages] AS p
			ON l.BUTTON_PAGEID = p.PAGE_ID
	WHERE
		p.PAGE_ID = @ID);
	
	SELECT COUNT(DISTINCT CLICK_USERID) FROM @Clicks;
END
GO